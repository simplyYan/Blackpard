# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//tools/json_schema_compiler/json_schema_api.gni")
import("//tools/json_schema_compiler/json_features.gni")
import("schemas.gni")

assert(enable_extensions)

generated_json_strings("nw_api") {
  sources = schema_sources
  configs = [ "//build/config:precompiled_headers" ]
  bundle_name = "nwjs"

  deps = schema_dependencies
}

# GYP version: chrome/browser/extensions/api/api.gyp:chrome_api_registration
function_registration("nw_api_registration") {
  sources = schema_sources
  impl_dir = "//content/nw/src/api"
  configs = [ "//build/config:precompiled_headers" ]
  bundle_name = "nwjs"

  deps = [
    # Different APIs include headers from these targets.
    "//content/public/browser",
    "//extensions/browser",

    # Different APIs include some headers from chrome/common that in turn
    # include generated headers from these targets.
    # TODO(brettw) this should be made unnecessary if possible.
    ":nw_api",
    "//skia",
    "//components/sync",
    "//third_party/metrics_proto",
    "//ui/accessibility:ax_enums_mojo",
  ]

  deps += schema_dependencies
}

generated_types("generated_api_types") {
  sources = schema_sources
}